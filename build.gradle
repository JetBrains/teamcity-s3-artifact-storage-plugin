

ext {
  versionNumber = project.hasProperty('versionNumber') ? property('versionNumber') : 'SNAPSHOT-' + new Date().format('yyyyMMddHHmmss')
  projectIds = ['group': 'teamcity-s3-storage', 'version': versionNumber, 'artifact': 's3-artifact-storage']
  teamcityVersion = project.hasProperty('teamcityVersion') ? property('teamcityVersion') : '2022.10'
  awsCoreVersion = project.hasProperty('awsCoreVersion') ? property('awsCoreVersion') : teamcityVersion
  teamcityS3SDKVersion = project.hasProperty('teamcityS3SDKVersion') ? property('teamcityS3SDKVersion') : teamcityVersion
  migrationToolVersion = project.hasProperty('migrationToolVersion') ? property('migrationToolVersion') : '2023.03.tcbuildtag'
  shouldIncludeMigrationTool = project.hasProperty('shouldIncludeMigrationTool') ? property('shouldIncludeMigrationTool') : false
}

group = projectIds.group
version = projectIds.version

allprojects {
  group = projectIds.group
  version = projectIds.version

  project.repositories.clear()

  repositories {
    mavenLocal()
    maven { url "https://download.jetbrains.com/teamcity-repository" }
    mavenCentral()
  }
}


subprojects {
  apply plugin: "java"

  test.useTestNG()
  jar.version = null

  dependencies{
    implementation("jetbrains.buildServer.util:amazon-util:$awsCoreVersion") {
      changing = true
      exclude group: 'com.fasterxml.jackson.core'
      exclude group: 'com.fasterxml.jackson.dataformat'
      exclude group: 'org.apache.httpcomponents'
    }
  }
}
