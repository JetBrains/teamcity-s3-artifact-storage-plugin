import java.nio.file.Paths
import com.github.jk1.license.render.JsonReportRenderer
import com.github.jk1.license.filter.LicenseBundleNormalizer

plugins {
    id "com.github.jk1.dependency-license-report" version "1.17"
}

initializeWorkspace()

ext {
  versionNumber = project.hasProperty('versionNumber') ? property('versionNumber') : 'SNAPSHOT-' + new Date().format('yyyyMMddHHmmss')
  projectIds = ['group': 'teamcity-s3-storage', 'version': versionNumber, 'artifact': 's3-artifact-storage']
  teamcityVersion = anyParam('teamcityVersion') ?: '2025.11-SNAPSHOT'
  awsCoreVersion = anyParam('awsCoreVersion') ?: teamcityVersion ?: 'SNAPSHOT'
  teamcityS3SDKVersion = project.hasProperty('teamcityS3SDKVersion') ? property('teamcityS3SDKVersion') : teamcityVersion
  spaceUsername = findProperty("space.repository.user") ?: findProperty("spaceUsername")
  spacePassword = findProperty("space.repository.password") ?: findProperty("spacePassword")
  localRepo = anyParamPath('TC_LOCAL_REPO')
}

group = projectIds.group
version = projectIds.version

allprojects {
  group = projectIds.group
  version = projectIds.version

  project.repositories.clear()

  repositories {
    mavenLocal()
    if (localRepo) {
      maven {
        name = "local-teamcity-artifacts"
        url "file:///${localRepo}"
      }
    }
    maven { url "https://repo.labs.intellij.net/teamcity" }
    maven { url "https://download.jetbrains.com/teamcity-repository" }
    maven { url "https://repo.labs.intellij.net/teamcity" }
    maven {
      url "https://packages.jetbrains.team/maven/p/tc/maven"
      credentials {
        username = spaceUsername as String
        password = spacePassword as String
      }
    }
    mavenCentral()
  }
}


subprojects {
  apply plugin: "java"
  apply plugin: 'java-library'

  test.useTestNG()
  jar.version = null

  dependencies{
    compileOnly ("org.jetbrains.teamcity.plugins:aws-core-common:$awsCoreVersion") {
      changing = true
      exclude group: 'com.fasterxml.jackson.core'
      exclude group: 'com.fasterxml.jackson.dataformat'
      exclude group: 'org.apache.httpcomponents'
      exclude group: 'org.jetbrains.teamcity'
    }
  }

  tasks.withType(JavaCompile) {
    sourceCompatibility = "1.8"
    targetCompatibility = "1.8"

    if (project.hasProperty('JDK_18')) {
      options.bootstrapClasspath = layout.files("$JDK_18/jre/lib/rt.jar")
    }
  }
}

licenseReport {
    renderers = [new JsonReportRenderer('third-party-libraries.json')]
    excludes = ["org.jetbrains.*", "com.jetbrains.*", ".*jackson-bom*"]
    filters = [new LicenseBundleNormalizer(bundlePath: "${project.rootDir}/license-third-party-normalizer.json", createDefaultTransformationRules: false)]
}

def anyParamPath(String... names) {
  def param = anyParam(names);
  if (param == null || param.isEmpty())
    return null
  return (Paths.get(param).isAbsolute()) ?
         Paths.get(param) : getRootDir().toPath().resolve(param)
}

def anyParam(String... names) {
  def param
  try {
    param = names.findResult {
      project.hasProperty(it) ? project.getProperty(it) : System.getProperty(it) ?: System.getenv(it) ?: null
    }
    if (param == null || param.isEmpty())
      param = null
  } finally {
    println("AnyParam: $names -> $param")
  }
  return param
}

def initializeWorkspace() {
  if (System.getProperty("idea.active") != null) {
    println "Attempt to configure workspace in IDEA"
    def coreVersionProperties = project.projectDir.toPath().parent.parent.resolve(".version.properties")
    if (coreVersionProperties.toFile().exists()) {
      def p = new Properties().tap {
        it.load(new FileInputStream(coreVersionProperties.toFile()))
      }
      p.forEach { k,v ->
        System.setProperty(k, v);
      }
    }
  }
}
