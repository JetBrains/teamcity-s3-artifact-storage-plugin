apply plugin: 'maven-publish'

ext {
  spaceRepositoryUser = findProperty('space.repository.user')
  spaceRepositoryPassword = findProperty('space.repository.password')
}

dependencies {
  compileOnly "org.jetbrains.teamcity:common-api:${teamcityVersion}"
  compileOnly 'commons-httpclient:commons-httpclient:3.1'
  implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml:2.10.5'
  compileOnly 'com.google.guava:guava:13.0.1'

  testCompile "org.testng:testng:6.8.21"
  testCompile "org.jetbrains.teamcity:tests-support:${teamcityVersion}"
  testCompile "org.mockito:mockito-core:3.9.0"
}

tasks.withType(JavaCompile) {
  sourceCompatibility = "1.8"
  targetCompatibility = "1.8"


  if (project.hasProperty('JDK_18')) {
    options.bootstrapClasspath = layout.files("$JDK_18/jre/lib/rt.jar")
  }
}


publishing {
  repositories {
    maven {
      name = "spacePackages"
      credentials {
        username = spaceRepositoryUser
        password = spaceRepositoryPassword
      }
      url = 'https://packages.jetbrains.team/maven/p/tc/maven'
    }
  }

  publications {
    mavenPrivate(MavenPublication) {
      groupId = 'org.jetbrains.teamcity.internal'
      artifactId = 'teamcity-s3-sdk'
      version = "${teamcityS3SDKVersion}"
      from components.java
      pom {
        name = 'TeamCity S3 SDK'
        description = 'Shared S3 SDK Library'
        url = 'https://packages.jetbrains.team/maven/p/tc/maven'
      }
    }
  }
}
