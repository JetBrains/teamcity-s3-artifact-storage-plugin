apply plugin: 'maven-publish'

ext {
  spaceRepositoryUser = findProperty('space.repository.user')
  spaceRepositoryPassword = findProperty('space.repository.password')
}

dependencies {
  compile "org.jetbrains.teamcity:common-api:${teamcityVersion}"
  compile 'commons-httpclient:commons-httpclient:3.1'
  compile 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml:2.10.5'
  compile("jetbrains.buildServer.util:amazon-util:${teamcityVersion}") {
    changing = true
  }
  compile "com.github.mwiede:jsch:0.1.67"

  testCompile "org.testng:testng:6.8.21"
  testCompile "org.jetbrains.teamcity:tests-support:${teamcityVersion}"
}

tasks.withType(JavaCompile) {
  sourceCompatibility = "1.8"
  targetCompatibility = "1.8"


  if (project.hasProperty('JDK_18')) {
    options.bootstrapClasspath = layout.files("$JDK_18/jre/lib/rt.jar")
  }
}


publishing {
  repositories {
    maven {
      name = "spacePackages"
      credentials {
        username = spaceRepositoryUser
        password = spaceRepositoryPassword
      }
      url = 'https://packages.jetbrains.team/maven/p/tc/maven'
    }
  }

  publications {
    mavenPrivate(MavenPublication) {
      groupId = 'org.jetbrains.teamcity.internal'
      artifactId = 'teamcity-s3-sdk'
      version = "${teamcityS3SDKVersion}"
      from components.java
      pom {
        name = 'TeamCity S3 SDK'
        description = 'Shared S3 SDK Library'
        url = 'https://packages.jetbrains.team/maven/p/tc/maven'
      }
    }
  }
}
